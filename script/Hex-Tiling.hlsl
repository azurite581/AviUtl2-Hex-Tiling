/*
Implementation based on:
Mikkelsen (2022) "Practical Real-Time Hex-Tiling"

- https://jcgt.org/published/0011/03/05/
- https://github.com/mmikk/hextile-demo

MIT License
Copyright (c) 2022 mmikk

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#pragma warning(disable : 3556)  // disable WAR_INT_DIVIDE_SLOW

Texture2D<float4> tex : register(t0);
SamplerState samp : register(s0);

cbuffer constant0 : register(b0) {
    float2 resolution;
    float size;
    float tileSize;
    float rotStrength;
    float r;
    float showWeights;
}

static const float M_PI = 3.14159265;
static const float g_fallOffContrast = 0.6;
static const float g_exp = 7.0;

void TriangleGrid(
    out float w1, out float w2, out float w3,
	out int2 vertex1, out int2 vertex2, out int2 vertex3,
	float2 st)
{
	// Scaling of the input
	st *= 2 * sqrt(3) / tileSize;

	// Skew input space into simplex triangle grid
	const float2x2 gridToSkewedGrid = float2x2(1.0, -0.57735027, 0.0, 1.15470054);
	float2 skewedCoord = mul(gridToSkewedGrid, st);

	int2 baseId = int2(floor(skewedCoord));
	float3 temp = float3(frac(skewedCoord), 0);
	temp.z = 1.0 - temp.x - temp.y;

	float s = step(0.0, -temp.z);
	float s2 = 2 * s-1;

	w1 = -temp.z * s2;
	w2 = s - temp.y * s2;
	w3 = s - temp.x * s2;

	vertex1 = baseId + int2(s, s);
	vertex2 = baseId + int2(s, 1 - s);
	vertex3 = baseId + int2(1 - s, s);
}

float2 hash(float2 p)
{
	float2 r = mul(float2x2(127.1, 311.7, 269.5, 183.3), p);
	return frac(sin(r) * 43758.5453);
}

float2 MakeCenST(int2 Vertex)
{
	float2x2 invSkewMat = float2x2(1.0, 0.5, 0.0, 1.0 / 1.15470054);
	return mul(invSkewMat, Vertex) / (2 * sqrt(3));
}

float2x2 LoadRot2x2(int2 idx, float rotStrength)
{
	float angle = abs(idx.x * idx.y) + abs(idx.x + idx.y) + M_PI;

	// remap to +/-pi
	angle = fmod(angle, 2 * M_PI);
	if(angle < 0) angle += 2 * M_PI;
	if(angle > M_PI) angle -= 2 * M_PI;

	angle *= rotStrength;

	float cs = cos(angle), si = sin(angle);

	return float2x2(cs, -si, si, cs);
}

float3 ProduceHexWeights(float3 W, int2 vertex1, int2 vertex2, int2 vertex3)
{
	float3 res = 0.0;

	int v1 = (vertex1.x-vertex1.y) % 3;
	if(v1 < 0) v1 += 3;

	int vh = v1 < 2 ? (v1 + 1) : 0;
	int vl = v1 > 0 ? (v1 - 1) : 2;
	int v2 = vertex1.x<vertex3.x ? vl : vh;
	int v3 = vertex1.x<vertex3.x ? vh : vl;

	res.x = v3 == 0 ? W.z : (v2==0 ? W.y : W.x);
	res.y = v3 == 1 ? W.z : (v2==1 ? W.y : W.x);
	res.z = v3 == 2 ? W.z : (v2==2 ? W.y : W.x);

	return res;
}

float3 Gain3(float3 x, float r)
{
	// increase contrast when r>0.5 and
	// reduce contrast if less
	float k = log(1 - r) / log(0.5);

	float3 s = 2 * step(0.5, x);
	float3 m = 2 * (1 - s);

	float3 res = 0.5 * s + 0.25 * m * pow(max(0.0, s + x * m), k);

	return res.xyz / (res.x + res.y + res.z);
}

void hex2colTex(
    out float4 color,
    out float3 weights,
    Texture2D tex,
    SamplerState samp,
    float2 st,
    float rotStrength,
    float r = 0.5
) {
    // Get triangle info.
    float w1, w2, w3;
    int2 vertex1, vertex2, vertex3;
    TriangleGrid(w1, w2, w3, vertex1, vertex2, vertex3, st);

    float2x2 rot1 = LoadRot2x2(vertex1, rotStrength);
    float2x2 rot2 = LoadRot2x2(vertex2, rotStrength);
    float2x2 rot3 = LoadRot2x2(vertex3, rotStrength);

    float2 cen1 = MakeCenST(vertex1);
    float2 cen2 = MakeCenST(vertex2);
    float2 cen3 = MakeCenST(vertex3);

    float2 st1 = mul(st - cen1, rot1) + cen1 + hash(vertex1);
    float2 st2 = mul(st - cen2, rot2) + cen2 + hash(vertex2);
    float2 st3 = mul(st - cen3, rot3) + cen3 + hash(vertex3);

    // Fetch input.
    float4 c1 = tex.Sample(samp, st1);
    float4 c2 = tex.Sample(samp, st2);
    float4 c3 = tex.Sample(samp, st3);

    // Use luminance as weight.
    float3 Lw = float3(0.299, 0.587, 0.114);
    float3 Dw = float3(
        dot(c1.xyz, Lw),
        dot(c2.xyz, Lw),
        dot(c3.xyz, Lw)
    );
    Dw = lerp(1.0, Dw, g_fallOffContrast);  // 0.6

    float3 W = Dw * pow(float3(w1, w2, w3), g_exp);  // 7
    W /= (W.x + W.y + W.z);

    if (r != 0.5)
        W = Gain3(W, r);

    color   = W.x * c1 + W.y * c2 + W.z * c3;
    weights = ProduceHexWeights(W.xyz, vertex1, vertex2, vertex3);
}

float4 psmain(float4 pos : SV_Position, float2 uv : TEXCOORD) : SV_Target
{
    float2 st = (pos.xy - 0.5 * resolution) / min(resolution.x, resolution.y);

    float4 color;
    float3 weights;
    hex2colTex(color, weights, tex, samp, st / size, rotStrength, r);

    return showWeights > 0.5 ? float4(weights, 1.0) : color;
}
